#!/usr/bin/env zsh

setopt ERR_EXIT
setopt NO_UNSET
setopt PIPE_FAIL

# ==============================================================================
# = Configuration                                                              =
# ==============================================================================

# Paths

root=$0:A:h:h

venv=$root/local/venv


# Packages

pacman_packages=(
  git
  python
  python-pyparsing
  zsh
)


# ==============================================================================
# = Tasks                                                                      =
# ==============================================================================

function install_pacman_packages()
{
    sudo pacman --noconfirm --sync --needed --refresh $pacman_packages
}

function create_virtualenv()
{
    virtualenv $venv
}

function install_python_packages()
{
    setopt LOCAL_OPTIONS

    unsetopt NO_UNSET
    source $venv/bin/activate
    setopt NO_UNSET

    pip install --requirement $root/requirements.txt
}


# ==============================================================================
# = Command line interface                                                     =
# ==============================================================================

tasks=(
    install_pacman_packages
    create_virtualenv
    install_python_packages
)

function usage()
{
    cat <<-'EOF'
		Set up a development environment

		Usage:

		    setup [TASK...]

		Tasks:

		    install_pacman_packages
		    create_virtualenv
		    install_python_packages
	EOF
    exit 1
}

for task in $@; do
    if (( ! ${+tasks[(r)$task]} )); then
        usage
    fi
done

for task in ${@:-$tasks}; do
    print -P -- "%F{green}Task: $task%f"
    $task
done

